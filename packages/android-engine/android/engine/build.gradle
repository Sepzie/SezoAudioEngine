plugins {
  id "com.android.library"
  id "org.jetbrains.kotlin.android"
  id "maven-publish"
}

group = "com.github.Sepzie"
version = System.getenv("VERSION") ?: "0.0.1"

android {
  namespace "com.sezo.audioengine"
  compileSdk 34
  ndkVersion "26.1.10909125"

  defaultConfig {
    minSdk 24
    targetSdk 34

    ndk {
      abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
    }

    externalNativeBuild {
      cmake {
        cppFlags "-std=c++17", "-frtti", "-fexceptions"
        arguments "-DANDROID_STL=c++_shared"
      }
    }
  }

  externalNativeBuild {
    cmake {
      path "src/main/cpp/CMakeLists.txt"
      version "3.22.1"
    }
  }

  buildTypes {
    release {
      minifyEnabled false
    }
    debug {
      minifyEnabled false
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  publishing {
    singleVariant("release")
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.0"
  implementation "androidx.annotation:annotation:1.7.1"
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
        groupId = project.group
        artifactId = "audio-engine"
        version = project.version
        pom {
          name = "Sezo Audio Engine"
          description = "Native Android audio engine for multi-track playback, pitch/speed control, recording, and extraction."
          url = "https://github.com/Sepzie/SezoAudioEngine"
          licenses {
            license {
              name = "MIT License"
              url = "https://opensource.org/licenses/MIT"
              distribution = "repo"
            }
          }
          developers {
            developer {
              id = "Sepzie"
              name = "Sepzie"
              url = "https://github.com/Sepzie"
            }
          }
          scm {
            connection = "scm:git:git://github.com/Sepzie/SezoAudioEngine.git"
            developerConnection = "scm:git:ssh://git@github.com:Sepzie/SezoAudioEngine.git"
            url = "https://github.com/Sepzie/SezoAudioEngine"
          }
        }
      }
    }
  }
}

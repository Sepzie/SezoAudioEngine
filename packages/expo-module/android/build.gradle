plugins {
  id "com.android.library"
  id "org.jetbrains.kotlin.android"
}

android {
  namespace "expo.modules.audioengine"
  compileSdk 34
  ndkVersion "26.1.10909125"

  defaultConfig {
    minSdk 24
    targetSdk 34
  }

  // Note: No native build needed here - we use the android-engine library
  // which provides libsezo_audio_engine.so through the Gradle dependency

  buildTypes {
    release {
      minifyEnabled false
    }
    debug {
      minifyEnabled false
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.0"
  implementation project(":expo-modules-core")

  // Link to the android-engine package when available, otherwise fall back to JitPack.
  def androidEngineProject = rootProject.findProject(":android-engine")
  if (androidEngineProject != null) {
    implementation androidEngineProject
  } else {
    def sezoAudioEngineVersion = project.findProperty("sezoAudioEngineVersion") ?: "android-engine-v0.1.4"
    implementation "com.github.Sepzie:SezoAudioEngine:${sezoAudioEngineVersion}"
  }
}
